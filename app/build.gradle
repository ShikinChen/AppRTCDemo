import com.sdk.versionplugin.*

apply plugin: 'com.android.application'

android {
    compileSdkVersion BuildConfig.compileSdkVersion

    defaultConfig {
        applicationId 'org.appspot.apprtc'
        minSdkVersion BuildConfig.minSdkVersion
        targetSdkVersion BuildConfig.targetSdkVersion
        versionCode 1
        versionName '1.0.0'

        buildConfigField("String", "HOST_ADDRESS", "\"$host_address\"")

        resValue("string", "pref_room_server_url_default", "http://${host_address}:8080")
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        doNotStrip "*/arm64-v8a/*.so"
        doNotStrip "*/x86_64/*.so"
    }
}

tasks.whenTaskAdded {
    theTask ->
        if (theTask.name == 'assembleDebug') {
            theTask.doLast {
                println "start_lldb"
                try {
                    exec {
                        commandLine 'bash', '-c',  "../start_lldb.sh"
                    }
                }
                catch(Exception e){
                    println("Exception: $e")
                }
            }
        }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation "com.android.support:support-annotations:${AndroidSupport.androidSupportSdkVersion}"
    implementation project(":webrtc")
}


